"fs" import
"terminal" import

main -> none fun
	filename "Source file : " :input =
	filename :fs.exists :not if
		"File not found\n" :print
		1 :exit
	fi
	sourceFile filename "r" :fs.open =
	pointer 0 =
	memory 0 1 list[int] =
	loopStack 0 list[int] =
	op true =
	op while
		op sourceFile :fs.get =
		op "+" == if
			memory pointer get memory pointer get 1 + =
		else op "-" == if
			memory pointer get memory pointer get 1 - =
		else op ">" == if
			pointer pointer 1 + =
			memory :len pointer <= if
				memory 0 :push
			fi
		else op "<" == if
			pointer 0 <= if
				"Error : pointer out of bounds\n" :print
				1 :exit
			fi
			pointer pointer 1 - =
		else op "." == if
			memory pointer get :chr :print
		else op "," == if
			"Input : " :print
			memory pointer get :getChr :ord =
		else op "[" == if
			memory pointer get 0 == if
				openBrackets 1 =
				sourceFile :fs.eof :not openBrackets 0 != :and while
					op sourceFile :fs.get =
					op "[" == if
						openBrackets openBrackets 1 + =
					else op "]" == if
						openBrackets openBrackets 1 - =
					fi fi
				elihw
				sourceFile :fs.eof if
					"Error : unmatched [\n" :print
					1 :exit
				fi
			else
				loopStack sourceFile :fs.tellg :push
			fi
		else op "]" == if
			loopStack :len 0 == if
				"Error : unmatched ]\n" :print
				1 :exit
			fi
			memory pointer get 0 == if
				loopStack :pop
			else
				sourceFile loopStack loopStack :len 1 - get true :fs.seekg
			fi
		fi fi fi fi fi fi fi fi
	elihw
	sourceFile :fs.close
nuf