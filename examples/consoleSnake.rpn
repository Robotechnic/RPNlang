"terminal" import
"time" import
"random" import

Point struct
	x -> int
	y -> int
tcurts

drawSnake list[Point] rings -> none fun
	top rings :len 1 - =
	rings top get->x rings top get->y terminal.YELLOW_BACKGROUND :terminal.setPixelColor
	i 0 top 1 for
		rings i get->x rings i get->y terminal.GREEN_BACKGROUND :terminal.setPixelColor
	rof
	:terminal.flush
nuf

drawApple Point pos -> none fun
	pos->x pos->y terminal.RED_BACKGROUND :terminal.setPixelColor
	:terminal.flush
nuf

moveSnake list[Point] rings int direction -> list[Point] fun
	top rings :len 1 - =
	direction 0 == if
		rings rings top get->x rings top get->y 1 - Point :push
	else direction 1 == if
		rings rings top get->x rings top get->y 1 + Point :push
	else direction 2 == if
		rings rings top get->x 1 - rings top get->y Point :push
	else direction 3 == if
		rings rings top get->x 1 + rings top get->y Point :push
	fi fi fi fi
	rings return
nuf

appleCollision list[Point] rings Point pos -> bool fun
	top rings :len 1 - =
	rings top get->x pos->x == rings top get->y pos->y == :and return
nuf

spawnApple list[Point] rings -> Point fun
	pos 1 :terminal.width :random.randRange 1 :terminal.height :random.randRange Point =
	rings pos :appleCollision if
		pos rings :spawnApple =
	fi
	pos return
nuf

snakeCollision list[Point] rings -> bool fun
	top rings :len 1 - =
	hx rings top get->x =
	hy rings top get->y =
	i 0 rings :len 1 - 1 for
		rings i get->x hx == rings i get->y hy == :and if
			true return
		fi
	rof
	false return
nuf

wallCollision list[Point] rings int width int height -> bool fun
	top rings :len 1 - =
	hx rings top get->x =
	hy rings top get->y =
	width :terminal.width =
	height :terminal.height =
	hx 0 <= hx width > :or hy 0 <= hy height > :or :or return
nuf

main -> none fun
	:random.rseed
	:terminal.clear
	:terminal.rawMode
	quit false =
	gameOver false =
	direction 0 =
	rings :terminal.width 2 / :terminal.height 2 / Point 1 list[Point] =
	i 0 2 1 for
		rings :terminal.width 2 / i + :terminal.height 2 / Point :push
	rof
	apple rings :spawnApple =
	last :time.time =
	quit :not gameOver :not :and while
		# keyboard management
		key :terminal.getKey =
		key "a" == if
			quit true =
		else key "z" == direction 1 != :and if
			direction 0 =
		else key "s" == direction 0 != :and if
			direction 1 =
		else key "q" == direction 3 != :and if
			direction 2 =
		else key "d" == direction 2 != :and if
			direction 3 =
		fi fi fi fi fi
		
		# game logic
		:time.time last - 150 < if
			continue
		fi
		last :time.time =
		:terminal.clear
		rings rings direction :moveSnake =
		rings apple :appleCollision if
			apple rings :spawnApple =
		else
			rings 0 :remove
		fi
		:terminal.width 2 / 9 - 1 "Press a key to quit" terminal.YELLOW terminal.DEFAULT_BACKGROUND :terminal.drawText
		scoreText rings :len 3 - f"Score: {}" =
		:terminal.width 2 / scoreText :len 2 / - 2 scoreText terminal.DEFAULT terminal.DEFAULT_BACKGROUND :terminal.drawText
		apple :drawApple 
		rings :drawSnake
		
		gameOver rings :snakeCollision =
		gameOver gameOver rings :terminal.width :terminal.height :wallCollision  :or =
	elihw
	:terminal.normalMode
	:terminal.clear
	:terminal.home
	gameOver if
		"Game Over\n" :print
	else
		"Goodbye\n" :print
	fi
	rings :len 3 - f"Your score is {}\n" :print
nuf

